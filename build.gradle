plugins {
    alias(libs.plugins.android.app).apply(false)
    alias(libs.plugins.android.lib).apply(false)
    alias(libs.plugins.kotlin.android).apply(false)
    alias(libs.plugins.kotlin.kapt).apply(false)
    alias(libs.plugins.dagger.hilt).apply(false)
    alias(libs.plugins.ktlint).apply(true)
    alias(libs.plugins.detekt).apply(true)
}

apply from: "buildscripts/githooks.gradle"

subprojects {
    apply from: "../buildscripts/ktlint.gradle"
    apply from: "../buildscripts/detekt.gradle"

    afterEvaluate { project ->
        if(!project.hasProperty("android")) return
        android {
            compileSdk 34
            defaultConfig {
                minSdk 24
                targetSdk 34
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }

            kotlinOptions {
                jvmTarget = '17'
            }

            buildFeatures {
                viewBinding = true
                buildConfig = true
            }

            packagingOptions {
                exclude 'META-INF/gradle/incremental.annotation.processors'
            }
        }
    }
}

tasks.getByPath("app:clean").dependsOn installHooks
